plugins {
	id 'java'
	id "architectury-plugin" version "1.3.48"
	id "forgified-fabric-loom" version "0.5.30" apply false
}
architect {
	minecraft = rootProject.minecraft_version
}

if(!JavaVersion.current().java11Compatible) {
	throw new RuntimeException("You must use Java 11 or above to compile sandbox")
}

subprojects {
	apply plugin: "java"
	if(name != "TestAddon") {
		apply plugin: "architectury-plugin"
		apply plugin: "forgified-fabric-loom"

		loom {
			silentMojangMappingsLicense()
		}
	}

	java {
		toolchain {
			languageVersion.set(JavaLanguageVersion.of(11))
		}
	}

	archivesBaseName = rootProject.archives_base_name
	version = rootProject.mod_version
	group = rootProject.maven_group

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}

	repositories {
		maven { url 'https://nexus.sandboxpowered.org/repository/maven-public/' }
		maven {
			name = "JitPack"
			url = "https://jitpack.io"
		}
	}

	dependencies {
		implementation enforcedPlatform('org.sandboxpowered.api:api:+')

		implementation('org.sandboxpowered.api:base')
		implementation('org.sandboxpowered.api:rendering')
		implementation('org.sandboxpowered.api:resources')

		compileOnly 'org.jetbrains:annotations:19.0.0'
		implementation( 'com.electronwill.night-config:core:3.6.0')
		implementation('com.electronwill.night-config:toml:3.6.0')
		implementation('com.github.zafarkhaja:java-semver:0.9.0')
		implementation('org.sandboxpowered:SimpleEventHandler:2.0.3')
		implementation 'com.google.guava:guava:21.0'
		implementation 'org.apache.commons:commons-lang3:3.9'
		implementation 'commons-io:commons-io:2.5'
	}
}