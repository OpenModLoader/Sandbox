plugins {
	id 'fabric-loom' version '0.2.7-SNAPSHOT'
	id 'java-library'
	id 'maven-publish'
	id "com.jfrog.bintray" version "1.8.4"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

def ENV = System.getenv()
archivesBaseName = project.archives_base_name
def buildNumber = ENV.BUILD_NUMBER
version = "$project.sandbox_version.${buildNumber ? (buildNumber) : "0"}"
group = project.maven_group

minecraft {
}

allprojects {
	apply plugin: 'java'
	repositories {
		mavenCentral()
		maven { url 'https://dl.bintray.com/sandboxpowered/API/' }
		maven { url 'https://maven.sandboxpowered.org/' }
	}

	dependencies {
		implementation platform("org.sandboxpowered.api:api:0.4.+")

		implementation "org.sandboxpowered.api:base"
		implementation "org.sandboxpowered.api:rendering"
		implementation "org.sandboxpowered.api:resources"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
	runtime('org.apache.logging.log4j:log4j-slf4j-impl:2.8.1') {
		transitive = false
	}
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

bintray {
	user = System.getenv("BINTRAY_USER")
	key = System.getenv("BINTRAY_KEY")
	publications = ['maven']
	publish = true

	pkg {
		repo = 'Loader'
		name = 'fabric'
		userOrg = 'sandboxpowered'
		vcsUrl = 'https://github.com/sandboxpowered/sandbox.git'
		publicDownloadNumbers = true
		githubRepo = 'sandboxpowered/sandbox'
		githubReleaseNotesFile = 'README.md'
		version {
			name = project.version
			desc = ''
			vcsTag = project.version
			released  = new Date()
		}
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			artifact jar
			artifact sourcesJar
		}
	}
}