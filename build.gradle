plugins {
	id 'fabric-loom' version '0.2.7-SNAPSHOT'
	id 'java-library'
	id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11

archivesBaseName = project.archives_base_name
version = "$project.sandbox_version.${System.getenv("TRAVIS_BUILD_NUMBER") ? (+System.getenv("TRAVIS_BUILD_NUMBER")) : "0"}"
group = project.maven_group

minecraft {
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.sandboxpowered.org/' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
	runtime('org.apache.logging.log4j:log4j-slf4j-impl:2.8.1') {
		transitive = false
	}

	implementation platform("org.sandboxpowered.api:api:0.4.+")

	implementation "org.sandboxpowered.api:base"
	implementation "org.sandboxpowered.api:rendering"
	implementation "org.sandboxpowered.api:rendering-opengl"
	implementation "org.sandboxpowered.api:rendering-vulkan"
	implementation "org.sandboxpowered.api:resources"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		maven(MavenPublication) {
			artifact jar
			artifact sourcesJar
		}
	}
	repositories {
		if (System.getenv("mvn_username")) {
			maven {
				name = "Maven"
				url = uri("sftp://maven.sandboxpowered.org:22/sbxmvn/")
				credentials {
					username = System.getenv("mvn_username")
					password = System.getenv("mvn_password")
				}
			}
		}
	}
}